# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgbase=vdr-epg-daemon
pkgname=('epgd' 'mariadb-epglv')
pkgver=1.1.146
pkgrel=3
url='http://projects.vdr-developer.org/projects/vdr-epg-daemon'
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('curl' 'git' 'imlib2' 'jansson' 'libarchive' 'libjpeg' 'libmariadbclient' 'libmicrohttpd' 'libxslt' 'python')
source=("https://projects.vdr-developer.org/git/vdr-epg-daemon.git/snapshot/$pkgbase-$pkgver.tar.bz2"
        'makefile.patch')
md5sums=('6a892aedd6af850be6880d702b9222d5'
         '7a025c01cb55f61d97d41d9398ec3683')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -p1 -i "$srcdir/makefile.patch"
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  make -j1 PREFIX=/usr
}

package_epgd() {
  pkgdesc='write epg data to a mysql database'
  depends=('curl' 'imlib2' 'jansson' 'libarchive' 'libmariadbclient' 'libmicrohttpd' 'libxslt' 'python')

  cd "$srcdir/$pkgbase-$pkgver"

  make -j1 \
    PREFIX=/usr \
    SYSTEMDDEST="$pkgdir/usr/lib/systemd/system" \
    DESTDIR="$pkgdir" \
    install-epgd install-epghttpd install-includes install-systemd

  cd $pkgdir
  backup=($(find etc -type f))
}

package_mariadb-epglv() {
  pkgdesc='epglv plugin for mysql/mariadb'
  depends=('libmariadbclient')
  backup=("usr/lib/mysql/plugin/mysqlepglv.so")
  install='mariadb-epglv.install'

  cd "$srcdir/$pkgbase-$pkgver/epglv"
  make -j1 PREFIX=/usr DESTDIR="$pkgdir" install
}
